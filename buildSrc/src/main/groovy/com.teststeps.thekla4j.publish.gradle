plugins {
    id 'signing'
    id 'maven-publish'
}

java {
    withJavadocJar()
    withSourcesJar()
}

tasks.named('sourcesJar') {
    duplicatesStrategy = 'include'
}

// ATTENTION: always set scmVersion properties before assigning the version to the version property
scmVersion {
    versionCreator { version, position ->
        version
    }
    versionIncrementer("incrementMinor")
    monorepo {
        include(rootProject.subprojects.collect({p -> p.name}))
    }
}

version = scmVersion.version

publishing {

    repositories {
        maven {
            def releasesRepoUrl = 'https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://central.sonatype.com/repository/maven-snapshots/'
            url = project.version.toString().endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username = project.findProperty('ossrhToken')
                password = project.findProperty('ossrhTokenPassword')
            }
        }
    }

    publications {

        pluginMaven(MavenPublication) {

            from components.java

            groupId = 'com.test-steps.thekla4j'
//            archivesName = "test-steps-thekla4j"
            version = project.version

            pom {
                name = 'thekla4j'
                description = 'a screenplay pattern implementation in java'
                url = 'https://github.com/andy-schulz/thekla4j'
                packaging = 'jar'

                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://github.com/andy-schulz/thekla4j/blob/master/LICENSE.md'
                    }
                }
                developers {
                    developer {
                        id = 'andy-schulz'
                        name = 'Andy Schulz'
                    }
                }
                scm {
                    connection = 'https://github.com/andy-schulz/thekla4j.git'
                    developerConnection = 'https://github.com/andy-schulz/thekla4j.git'
                    url = 'https://github.com/andy-schulz/thekla4j'
                }
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.pluginMaven
}


